// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - passes:
    - vert: sprite-vs:vert
      frag: sprite-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        alphaThreshold: { value: 0.5 }

        ## 自定义参数
        tt: { value: 0.0 }
        noiseTexture: { value: white, editor: { tooltip: "噪声贴图"} }
}%

CCProgram sprite-vs %{
  precision highp float;
  #include <builtin/uniforms/cc-global>
  #if USE_LOCAL
    #include <builtin/uniforms/cc-local>
  #endif
  #if SAMPLE_FROM_RT
    #include <common/common-define>
  #endif
  in vec3 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;

  out vec4 v_color;
  out vec2 v_uv0;

  vec4 vert () {
    vec4 pos = vec4(a_position, 1);

    #if USE_LOCAL
      pos = cc_matWorld * pos;
    #endif

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    v_uv0 = a_texCoord;
    #if SAMPLE_FROM_RT
      CC_HANDLE_RT_SAMPLE_FLIP(v_uv0);
    #endif
    v_color = a_color;

    return pos;
  }
}%

CCProgram sprite-fs %{
  precision highp float;
  #include <builtin/internal/embedded-alpha>
  #include <builtin/internal/alpha-test>

  in vec4 v_color;

  #if USE_TEXTURE
    in vec2 v_uv0;
    #pragma builtin(local)
    layout(set = 2, binding = 11) uniform sampler2D cc_spriteTexture;
  #endif

  uniform MXD {
    float tt;
  };

  uniform sampler2D noiseTexture;

  #pragma define PI 3.1415
  #pragma define S(x,y,z) smoothstep(x,y,z)
  #pragma define B(x,y,z,b) S(x, x+b, z)*S(y+b, y, z)
  #pragma define saturate(x) clamp(x,0.,1.)
  #pragma define MOD3 vec3(.1031,.11369,.13787)
  #pragma define MOONPOS vec2(1, .8)

  float fbm(vec2 p) {
    // return 1.;
    return  0.5000 * texture(noiseTexture, p * 1.00).x +
            0.2500 * texture(noiseTexture, p * 2.02).x +
            0.1250 * texture(noiseTexture, p * 4.03).x +
            0.0625 * texture(noiseTexture, p * 8.04).x;
  }

  //  1 out, 1 in...
  float hash11(float p) {
    // From Dave Hoskins
    vec3 p3  = fract(vec3(p) * MOD3);
    p3 += dot(p3, p3.yzx + 19.19);
    return fract((p3.x + p3.y) * p3.z);
  }

  //  1 out, 2 in...
  float hash(vec2 p) {
    vec3 p3  = fract(vec3(p.xyx) * MOD3);
      p3 += dot(p3, p3.yzx + 19.19);
      return fract((p3.x + p3.y) * p3.z);
  }

  float hash2(vec2 p) {
    vec2 q = p;
      q *= 16.;
      q += 52351.;
      float v = 0.5*hash(vec2(floor(q.x),floor(q.y)));
      q *= 100.;
      q += 15367.;
      v += 0.25*hash(vec2(floor(q.x),floor(q.y)));
      q *= 200.;
      q += 35278.;
      v += 0.165*hash(vec2(floor(q.x),floor(q.y)));
      return v;
  }

  float remap(float a, float b, float c, float d, float t) {
    return ((t-a) / (b-a)) * (d-c) + c;
  }

  vec4 moon(vec3 col) {
    float t = 0.;
    vec2 p= v_uv0;
    p.x *= 960. / 540.;
    p-=.5;
    float f = fbm( 0.002*vec2(p.x,1.0)/p.y );
    
    // moon
    float d = length(p);
    vec3 moon = vec3(0.98,0.97,0.95)*(1.0-0.1*smoothstep(0.2,0.5,f));
    col += 0.8*moon*exp(-4.0*d)*vec3(1.1,1.0,0.8);
    col += 0.2*moon*exp(-2.0*d);
    moon *= hash2(p);
    col = mix( col, moon, 1.0-smoothstep(0.2,0.215,d) );
    return vec4(col,1.);
  }

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      o *= CCSampleWithAlphaSeparated(cc_spriteTexture, v_uv0);
      #if IS_GRAY
        float gray  = 0.2126 * o.r + 0.7152 * o.g + 0.0722 * o.b;
        o.r = o.g = o.b = gray;
      #endif
    #endif

    vec3 col = vec3(.1, .15, .2);
    // draw star
    float alpha = pow(hash(v_uv0 * 10000.), 20.) * pow(hash(v_uv0 * 11111.), 20.);
    float twinkle = (1. + sin(tt * 2.5 + (hash(v_uv0 * 35785.) * hash(v_uv0 * 24586.)) * 314.23)) / 2.;
    col += alpha * twinkle;

    // draw moon 
    vec2 uv = v_uv0;
    uv.x *=  960. / 640.;
    uv.x -= 1.2;
    uv.y -= .3;
    float d = length(uv);
    vec3 moon = vec3(0.98, 0.97, 0.95);
    col += 0.7 * moon * exp(-4.0 * d) * vec3(1.1, 1.0, 0.8);
    //moon *= 0.85 + 0.15* hash2(v_uv0);
    col = mix( col, moon, 1.0-smoothstep(0.1, 0.110, d));
    o = vec4(col, 1.);

    o *= v_color;
    ALPHA_TEST(o);
    return o;
  }
}%